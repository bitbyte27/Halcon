
## 分享一个摄像头解码二维码的实例及分步骤注解

> [Halcon机器视觉二维码实例及分步注解](https://blog.csdn.net/bitezijie/article/details/24786997)
<br>

<pre name="code" class="halcon">
dev_close_window ()
dev_open_window (0, 0, 400, 400, 'black', WindowHandle)
* 先关闭活动图形窗口，再打开这个窗口，标识符为WindowHandle； 
* 相对于界面左上角第0行、第0列，大小为400×400像素，颜色为黑色。
</pre>

<pre name="code" class="halcon">
open_framegrabber ('DirectShow', 1, 1, 0, 0, 0, 0, 'default', 8, 'rgb', -1, 'false', 'default', 'Gsou USB2.0 Camera', 0, -1, AcqHandle) 
* 打开帧接收器（图像采集设备，如摄像头，工业相机等），参数（Parameter）详见这个算子 
* 注意摄像头的名称,可以用工具栏中的“助手”——打开新的Image Acquisition获取摄像头及插入代码 
</pre>

<pre name="code" class="halcon">
grab_image_start (AcqHandle, -1)
while (true)

    grab_image_async (Image, AcqHandle, -1)
    create_data_code_2d_model ('QR Code', [], [], DataCodeHandle)
    * 二维码的创建开头的算子，clear为结束清除的算子，见下。 
  
      set_display_font (WindowHandle, 16, 'mono', 'true', 'false')
      dev_set_color ('forest green')
      dev_set_draw ('margin')
      dev_set_line_width (3)
      set_data_code_2d_param (DataCodeHandle, 'default_parameters', 'enhanced_recognition') 
      * 设置选定参数的二维数据模型，参数详见这个算子
  
      find_data_code_2d (Image, SymbolXLDs, DataCodeHandle, [], [], ResultHandles, DecodedDataStrings) 
      * 检测和读取二维代码符号，也支持读取二维数据模型的序列，参数详见这个算子 
      
      for i := 0 to |ResultHandles| - 1 by 1
            select_obj (SymbolXLDs, SymbolXLD, i+1)
            get_contour_xld (SymbolXLD, Row, Col)
            get_string_extents (WindowHandle, DecodedDataStrings, Ascent, Descent, TxtWidth, TxtHeight)
            disp_message (WindowHandle, DecodedDataStrings, 'image', max(Row-50), max([min(Col+30)-TxtWidth/2,1]), 'black', 'true')
      endfor 
      * 这段for循环语句的目的是让解码到的字符串（二维码的内容）显示到二维码深绿色（forest green上面定义）的解码区域框的行列位置。 
      * disp_message (WindowHandle, DecodedDataStrings, 'window', 12, 12, 'black', 'true') 
      * 如果不需要设置显示到区域框中间的位置，而是显示到窗体的上方或其他位置，那么不需要上面那段for语句，只需这段信息显示的语句即可显示到窗体相应位置。 
      
      if (|DecodedDataStrings|>0)
            disp_continue_message (WindowHandle, 'black', 'true')
            stop()
      endif
      * if语句，当解码一个（大于0，可设置多个）二维码就暂停摄像头获取图像，直至按F5. 

    clear_data_code_2d_model (DataCodeHandle)
    
endwhile
close_framegrabber (AcqHandle) 
</pre>

<img src="image003.jpg" align = "left"/>
